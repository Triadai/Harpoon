dnl Process this file with autoconf to produce a configure script.

dnl Turning off cache for debug reasons
dnl define([AC_CACHE_LOAD], )dnl
dnl define([AC_CACHE_SAVE], )dnl

AC_INIT(java/lang/System.java)

AC_CANONICAL_TARGET
if expr match ${target_cpu} .*86 > /dev/null; then target_cpu=x86; fi

PACKAGE="classpath"
#VERSION="0.05"
VERSION="0.05+cvs"
dnl We will not track/change lib version until we reach version 1.0
dnl at which time we'll have to be more anal about such things
LIBVERSION="0:0:0"
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(LIBVERSION)
AM_INIT_AUTOMAKE(${PACKAGE}, ${VERSION})

AM_CONFIG_HEADER(include/config.h)

AC_PREFIX_DEFAULT(/usr/local/classpath)

dnl required if SUBDIRS is used
AC_PROG_MAKE_SET

AC_ARG_ENABLE(java,
[  --enable-java           compile Java source [default=yes]],
[case "${enableval}" in 
  yes) COMPILE_JAVA=yes ;;
  no) COMPILE_JAVA=no ;;
  *) COMPILE_JAVA=yes ;;
esac],
[COMPILE_JAVA=yes])

AC_ARG_ENABLE(jni,
[  --enable-jni            compile JNI source [default=no]],
[case "${enableval}" in 
  yes) 
    COMPILE_JNI=yes
    COMPILE_JAVA=yes

    ;;
  no) COMPILE_JNI=no ;;
  *) COMPILE_JNI=yes; COMPILE_JAVA=yes ;;
esac],
[COMPILE_JNI=no])

AC_ARG_ENABLE(cni,
[  --enable-cni            compile CNI source [default=no]],
[case "${enableval}" in 
  yes) COMPILE_CNI=yes; COMPILE_JAVA=yes ;;
  no) COMPILE_CNI=no ;;
  *) COMPILE_CNI=yes; COMPILE_JAVA=yes ;;
esac],
[COMPILE_CNI=no])

dnl -----------------------------------------------------------
dnl GTK native peer
dnl -----------------------------------------------------------
AC_ARG_ENABLE(gtk-peer,
[  --enable-gtk-peer       compile GTK native peers [default=yes]],
[case "${enableval}" in
  yes) COMPILE_GTK_PEER=yes ;;
  no) COMPILE_GTK_PEER=no ;;
  *) COMPILE_GTK_PEER=yes ;;
esac],
[COMPILE_GTK_PEER=yes])

AM_CONDITIONAL(CREATE_JNI_HEADERS, test "x${COMPILE_JNI}" = xyes) 
AM_CONDITIONAL(CREATE_JNI_LIBRARIES, test "x${COMPILE_JNI}" = xyes)
AM_CONDITIONAL(CREATE_CNI_LIBRARIES, test "x${COMPILE_CNI}" = xyes)
AM_CONDITIONAL(CREATE_GTK_PEER_LIBRARIES, test "x${COMPILE_GTK_PEER}" = xyes)

AC_PROG_LN_S
AC_PROG_INSTALL

dnl Checks for programs.
  AC_PREREQ(2.52)

  dnl AC_PROG_CXX
  dnl Initialize libtool
  AM_DISABLE_STATIC
  AM_PROG_LIBTOOL
  dnl AC_PROG_AWK
  AC_PROG_CC
  AC_PROG_CPP

if test "x${COMPILE_JNI}" = xyes; then
  AC_HEADER_STDC
  AC_CHECK_HEADERS(unistd.h sys/types.h sys/config.h sys/ioctl.h asm/ioctls.h inttypes.h stdint.h)
  AC_EGREP_HEADER(uint32_t, stdint.h, AC_DEFINE(HAVE_INT32_DEFINED, 1, [Define to 1 if you have uint32_t]))
  AC_EGREP_HEADER(uint32_t, inttypes.h, AC_DEFINE(HAVE_INT32_DEFINED, 1, [Define to 1 if you have uint32_t]))
  AC_EGREP_HEADER(u_int32_t, sys/types.h, AC_DEFINE(HAVE_BSD_INT32_DEFINED, 1, [Define to 1 if you have BSD u_int32_t]))
  AC_EGREP_HEADER(u_int32_t, sys/config.h, AC_DEFINE(HAVE_BSD_INT32_DEFINED, 1, [Define to 1 if you have BSD u_int32_t]))

  AC_CHECK_FUNCS(gethostname socket strerror fork pipe execve open close lseek \
	fstat read write htonl memset htons connect getsockname sizeof getpeername \
  	bind listen accept recvfrom send sendto setsockopt getsockopt time mktime)

  AC_HEADER_TIME
  AC_STRUCT_TM
  AC_STRUCT_TIMEZONE

  AC_C_CONST

  dnl FIXME - does not allow cross compiling
  AC_C_BIGENDIAN

  AC_CYGWIN
  AC_MINGW32

  dnl We want to avoid -O2 at the moment, gcc 2.96 from Red Hat 7
  dnl cannot compile some native functions (gcc errors) with this
  dnl flag.
  CFLAGS="-g" 

  dnl We want warnings, lots of warnings :-)
  if test "x${GCC}" = xyes; then
    JAPHAR_GREP_CFLAGS(-Wall, [ CFLAGS="$CFLAGS -Wall" ])
  fi

  dnl Check for AWT related glib/gtk/libart_lgpl
  if test "x${COMPILE_GTK_PEER}" = xyes; then
    AM_PATH_GTK_2_0(2.0.0,,exit 1)
    AM_PATH_GLIB_2_0(2.0.0,,exit 1,gthread)
    dnl XXX Fix me when libart.m4 has the compile test fixed!
    dnl enable_libarttest=no
    AM_PATH_LIBART(2.1.0,,exit 1)
  fi

fi

CLASSPATH_WITH_JAVAH

CLASSPATH_WITH_INCLUDEDIR

if test "x${COMPILE_JNI}" = xyes; then
  AC_MSG_CHECKING("jni_md.h support")
  if test -f ${srcdir}/include/jni_md-${target_cpu}-${target_os}.h; then
    AC_MSG_RESULT("yes")
  else
    target_cpu=x86
    target_os=linux-gnu 
    AC_MSG_WARN("no, using x86-linux-gnu")
  fi
  AC_LINK_FILES(include/jni_md-${target_cpu}-${target_os}.h, include/jni_md.h)
fi

CLASSPATH_FIND_JAVA
CLASSPATH_FIND_JAVAC

CLASSPATH_WITH_CLASSLIB


dnl -----------------------------------------------------------
dnl Initialize maintainer mode
dnl -----------------------------------------------------------
AM_MAINTAINER_MODE

dnl -----------------------------------------------------------
dnl Enable debugging statements at compile time.  By default
dnl these statements should be optimized out of the bytecode
dnl produced by an optimizing Java compiler and not hinder 
dnl performance because debugging is turned off by default.
dnl -----------------------------------------------------------
AC_ARG_ENABLE(debug,
[  --enable-debug          enable runtime debugging code],
[case "${enableval}" in 
  yes) 
	LIBDEBUG="true"
        AC_DEFINE(DEBUG, 1, [Define to 1 if you want native library runtime debugging code enabled]) 
	;;
  no)   LIBDEBUG="false" ;;
  *) 	AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],
[LIBDEBUG="false"])
AC_SUBST(LIBDEBUG)
dnl -----------------------------------------------------------


dnl -----------------------------------------------------------
dnl Enable execution of all static initializer loadLibrary() 
dnl calls at compile time.  By default most people will not
dnl want to disable this, but some VMs (gcj) don't need or want 
dnl them.
dnl -----------------------------------------------------------
AC_ARG_ENABLE(load-library,
[  --enable-load-library   enable to use JNI native methods [default=yes]
                          (disabled automatically using --enable-cni)],
[case "${enableval}" in 
  yes) INIT_LOAD_LIBRARY="true" ;;
  no) INIT_LOAD_LIBRARY="false" ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-load-library) ;;
esac],
[
  if test "x${COMPILE_CNI}" = xyes; then
    INIT_LOAD_LIBRARY="false"
  else
    INIT_LOAD_LIBRARY="true"
  fi
])
AC_SUBST(INIT_LOAD_LIBRARY)
dnl -----------------------------------------------------------


dnl -----------------------------------------------------------
dnl Small loop to add other things to EXTRA_INCLUDES from a VM
dnl -----------------------------------------------------------
dnl for i in $JAPHAR_CFLAGS $KAFFE_CFLAGS ; do
dnl   add_to_extra_includes=1
dnl   for j in $EXTRA_INCLUDES ; do
dnl     if test $i = $j ; then
dnl       add_to_extra_includes=0
dnl     fi
dnl   done
dnl   if test $add_to_extra_includes -eq 1; then
dnl     EXTRA_INCLUDES="$EXTRA_INCLUDES $i"
dnl   fi
dnl done
dnl -----------------------------------------------------------


dnl Create a link to the VM specific files we're compiling with
if test "x${JVM_REFERENCE}" = x; then
  JVM_REFERENCE=reference
fi
AC_LINK_FILES(vm/$JVM_REFERENCE, vm/current)

dnl avoiding automake complaints
REMOVE=""
AC_SUBST(REMOVE)

dnl This is probably useless.
AC_PATH_PROG(SH, sh)
AC_PATH_PROG(MKDIR, mkdir)
AC_PATH_PROG(CP, cp)
AC_PATH_PROG(DATE, date)

dnl According to the GNU coding guide, we shouldn't require find, 
dnl and zip, however GNU provides both so it should be okay 
AC_PATH_PROG(FIND, find)

dnl Useful for disabling zip even if you have it
CLASSPATH_WITH_ZIP

CLASSPATH_ENABLE_GJDOC

dnl -----------------------------------------------------------
dnl -----------------------------------------------------------
AC_ARG_ENABLE(portable-native-sync,
[  --enable-portable-native-sync  synchronize VM threads portably],
[case "${enableval}" in 
  yes) 
       AC_DEFINE(PORTABLE_NATIVE_SYNC, 1, [Define to 1 if you want to synchronize VM threads portably])
       ;;

  no)  ;;

  *)   
       AC_MSG_ERROR(bad value ${enableval} for --enable-portable-native-sync) 
       ;;
esac],
[])

dnl -----------------------------------------------------------

AC_OUTPUT(Makefile
doc/Makefile
doc/api/Makefile
gnu/Makefile
gnu/classpath/Makefile
gnu/classpath/Configuration.java
gnu/java/Makefile
gnu/java/awt/Makefile
gnu/java/awt/image/Makefile
gnu/java/awt/peer/Makefile
gnu/java/awt/peer/gtk/Makefile
gnu/java/beans/Makefile
gnu/java/beans/editors/Makefile
gnu/java/beans/info/Makefile
gnu/java/io/Makefile
gnu/java/io/decode/Makefile
gnu/java/io/encode/Makefile
gnu/java/lang/Makefile
gnu/java/lang/reflect/Makefile
gnu/java/locale/Makefile
gnu/java/net/Makefile
gnu/java/net/content/Makefile
gnu/java/nio/Makefile
gnu/java/nio/charset/Makefile
gnu/java/net/content/text/Makefile
gnu/java/net/protocol/Makefile
gnu/java/net/protocol/file/Makefile
gnu/java/net/protocol/http/Makefile
gnu/java/net/protocol/jar/Makefile
gnu/java/math/Makefile
gnu/java/rmi/Makefile
gnu/java/rmi/dgc/Makefile
gnu/java/rmi/registry/Makefile
gnu/java/rmi/rmic/Makefile
gnu/java/rmi/server/Makefile
gnu/java/security/Makefile
gnu/java/security/der/Makefile
gnu/java/security/provider/Makefile
gnu/java/security/util/Makefile
gnu/java/security/x509/Makefile
gnu/java/text/Makefile
gnu/java/util/Makefile
gnu/java/util/prefs/Makefile
gnu/javax/Makefile
gnu/javax/rmi/Makefile
gnu/javax/rmi/CORBA/Makefile
include/jni.h
java/Makefile
java/applet/Makefile
java/awt/Makefile
java/awt/color/Makefile
java/awt/datatransfer/Makefile
java/awt/dnd/Makefile
java/awt/dnd/peer/Makefile
java/awt/event/Makefile
java/awt/font/Makefile
java/awt/geom/Makefile
java/awt/im/Makefile
java/awt/im/spi/Makefile
java/awt/image/Makefile
java/awt/image/renderable/Makefile
java/awt/peer/Makefile
java/awt/print/Makefile
java/beans/Makefile
java/beans/beancontext/Makefile
java/io/Makefile
java/lang/Makefile
java/lang/ref/Makefile
java/lang/reflect/Makefile
java/math/Makefile
java/net/Makefile
java/nio/Makefile
java/nio/channels/Makefile
java/nio/channels/spi/Makefile
java/nio/charset/Makefile
java/nio/charset/spi/Makefile
java/rmi/Makefile
java/rmi/activation/Makefile
java/rmi/dgc/Makefile
java/rmi/registry/Makefile
java/rmi/server/Makefile
java/security/Makefile
java/security/acl/Makefile
java/security/cert/Makefile
java/security/interfaces/Makefile
java/security/spec/Makefile
java/sql/Makefile
java/text/Makefile
java/util/Makefile
java/util/logging/Makefile
java/util/jar/Makefile
java/util/prefs/Makefile
java/util/regex/Makefile
java/util/zip/Makefile
javax/Makefile
javax/accessibility/Makefile
javax/naming/Makefile
javax/naming/directory/Makefile
javax/naming/event/Makefile
javax/naming/ldap/Makefile
javax/naming/spi/Makefile
javax/print/Makefile
javax/print/attribute/Makefile
javax/rmi/Makefile
javax/rmi/CORBA/Makefile
javax/security/Makefile
javax/security/auth/Makefile
javax/security/auth/x500/Makefile
javax/sql/Makefile
javax/swing/Makefile
javax/swing/border/Makefile
javax/swing/colorchooser/Makefile
javax/swing/event/Makefile
javax/swing/filechooser/Makefile
javax/swing/plaf/Makefile
javax/swing/plaf/basic/Makefile
javax/swing/plaf/metal/Makefile
javax/swing/table/Makefile
javax/swing/text/Makefile
javax/swing/text/html/Makefile
javax/swing/text/html/parser/Makefile
javax/swing/tree/Makefile
javax/swing/undo/Makefile
javax/transaction/Makefile
javax/transaction/xa/Makefile
native/Makefile
native/fdlibm/Makefile
native/cni/Makefile
native/jni/Makefile
native/jni/classpath/Makefile
native/jni/java-io/Makefile
native/jni/java-lang/Makefile
native/jni/java-net/Makefile
native/jni/java-util/Makefile
native/jni/gtk-peer/Makefile
resource/Makefile
resource/gnu/Makefile
resource/gnu/java/Makefile
resource/gnu/java/awt/Makefile
resource/gnu/java/awt/peer/Makefile
resource/gnu/java/awt/peer/gtk/Makefile
resource/java/Makefile
resource/java/util/Makefile
resource/java/security/Makefile
vm/Makefile
vm/reference/Makefile
vm/reference/java/Makefile
vm/reference/java/lang/Makefile
vm/reference/java/lang/reflect/Makefile
vm/reference/java/io/Makefile
lib/Makefile
lib/gen-classlist.sh
lib/gen_nio.sh,
[ chmod 755 lib/gen-classlist.sh
])


